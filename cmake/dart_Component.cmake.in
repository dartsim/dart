set("@PROJECT_NAME@_@component@_DEPENDENCIES" @internal_dependencies@)
set("@PROJECT_NAME@_@component@_LIBRARIES" @libraries@)

set_property(DIRECTORY PROPERTY "@PROJECT_NAME_UPPERCASE@_@component@_FOUND" TRUE)

if("@component@" STREQUAL "collision-bullet" OR "@component@" STREQUAL "collision-ode")
  if(NOT @PROJECT_NAME_UPPERCASE@_FIND_QUIETLY)
    message(DEPRECATION
      "DART CMake component '@component@' is a deprecated stub in DART 7: Bullet/ODE backends are part of the 'dart' core component. "
      "This component will be removed in DART 8. Stop requesting '@component@' and depend on component 'dart' instead."
    )
  endif()
endif()

foreach(external_dep @external_dependencies@)
  string(TOLOWER "${external_dep}" external_dep_lower)
  include(${CMAKE_CURRENT_LIST_DIR}/dart_find_${external_dep_lower}.cmake)

  string(TOUPPER ${external_dep} EXTERNAL_DEP_UPPER)
  if(NOT ${external_dep}_FOUND AND NOT ${EXTERNAL_DEP_UPPER}_FOUND)
    set_property(DIRECTORY PROPERTY @PROJECT_NAME_UPPERCASE@_@component@_FOUND FALSE)
    if(NOT @PROJECT_NAME_UPPERCASE@_FIND_QUIETLY)
      message(WARNING
        "Cannot retrieve @PROJECT_NAME@-@component@ because the dependency "
        "${external_dep} could not be found. This usually indicates a broken "
        "installation.")
    endif()
  endif()

endforeach()
